<languageVersion: 1.0;>
kernel HomographyTransform
<
	namespace: "net.saqoosha";
	vendor: "Saqoosha";
	version: 1;
	description: "Homography Transform";
>
{
	input image4 source;
	output pixel4 target;
	
	parameter float A;
	parameter float B;
	parameter float C;
	parameter float D;
	parameter float E;
	parameter float F;
	parameter float G;
	parameter float H;
	
	parameter int outWidth
	<
		minValue: int(1);
		defaultValue: int(256);
		description: "width of output image";
	>;
	
	parameter int outHeight
	<
		minValue: int(1);
		defaultValue: int(256);
		description: "height of output image";
	>;
	
	void evaluatePixel()
	{
		float u = outCoord().x / float(outWidth);
		float v = outCoord().y / float(outHeight);
		float w = (G * u + H * v + 1.0);
		float px = (A * u + B * v + C) / w;
		float py = (D * u + E * v + F) / w;
		target = sampleLinear(source, float2(px, py));
	}
}
